PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dummy: <http://rdf.biosemantics.org/ontologies/dummy/>
PREFIX rdcMeta: <http://rdf.biosemantics.org/ontologies/rd-connect/>
PREFIX iao: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>

SELECT DISTINCT ?sample ?sampleURI ?sampleType ?sampleTypeURI ?biobank ?biobankURI ?disease ?diseaseURI ?registry ?registryURI {
  
 VALUES(?phenotypeInput ?sampleTypeURI) {(<#phenotype#> <#sampleType#>)}  
 
 ?phenotypeURI  rdfs:subClassOf* ?phenotypeInput.
 
  GRAPH ?registryDistributionURI {    
    ?donorURI dummy:hasDisease ?diseaseURI;             
             rdcMeta:59e1324d_567b_42e1_bc88_203004e660da ?phenotypeURI.
  }   
  
  GRAPH ?biobankDistributionURI {    
    ?sampleURI a  ?sampleTypeURI;    
               rdcMeta:e297332a_00a9_4ed0_b661_00dbd35aff95 ?donorURI.
    
    ?sampleIdURI a rdcMeta:21f6df30_1f72_45fb_bfc1_2b3d1af1410a ;
                    dct:identifier ?sample;	
                    iao:IAO_0000219 ?sampleURI.
  }    
  {    
    ?biobankURI dct:title ?biobank;
                      dcat:distribution ?biobankDistributionURI. 
    ?registryURI dct:title ?registry;
                      dcat:distribution ?registryDistributionURI. 
    ?sampleTypeURI rdfs:label ?sampleType.    
    ?diseaseURI rdfs:label ?disease.  
  }
}